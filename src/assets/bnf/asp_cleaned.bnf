<program>            ::= <statements> <query>
                        | <statements>
                        | <query> 
<statements>         ::= <COMMENT> <BLANK> <statements> <BLANK> 
                        | <COMMENT> <BLANK>
                        | <statement> <BLANK> <statements> <BLANK> 
                        | <statement> <BLANK>
                        | ""
<query>              ::= <COMMENT> <BLANK> <statements><BLANK> 
                        | <COMMENT> <BLANK>
                        | <classical_literal> <QUERY_MARK> <BLANK>
<statement_head_cons_body> ::= <head> <CONS> <body> <DOT> 
<statement_head_cons> ::= <head> <CONS> <DOT> 
<statement_head>     ::= <head> <DOT> 
<statement>          ::= <statement_head_cons_body>
                        | <statement_head_cons>
                        | <statement_head>
                        | <CONS> <body> <DOT>
                        | <CONS> <DOT>
                        | <WCONS> <DOT> <SQUARE_OPEN> <weight_at_level> <SQUARE_CLOSE>
                        | <WCONS> <body> <DOT> <SQUARE_OPEN> <weight_at_level> <SQUARE_CLOSE>
<head>               ::= <disjunction>
                        | <choice>
<body_1>             ::= <naf_literal>
                        | <NAF> <aggregate>
<body_2>             ::= <naf_literal>
                        | <aggregate>
<body>               ::= <body_1> <COMMA> <body>
                        | <body_2> <COMMA> <body>
                        | <body_1>
                        | <body_2>
<disjunction>        ::= <classical_literal> <OR> <disjunction>
                        | <classical_literal>
<choice>             ::= <CURLY_OPEN> <choice_elements> <CURLY_CLOSE> <binop> <term>
                        | <CURLY_OPEN> <choice_elements> <CURLY_CLOSE>
                        | <CURLY_OPEN> <CURLY_CLOSE> <binop> <term>
                        | <CURLY_OPEN> <CURLY_CLOSE>
                        | <term> <binop> <CURLY_OPEN> <choice_elements> <CURLY_CLOSE> <binop> <term>
                        | <term> <binop> <CURLY_OPEN> <choice_elements> <CURLY_CLOSE>
                        | <term> <binop> <CURLY_OPEN> <CURLY_CLOSE> <binop> <term>
                        | <term> <binop> <CURLY_OPEN> <CURLY_CLOSE>
<choice_elements>    ::= <choice_element> <SEMICOLON> <choice_elements>
                        | <choice_element>
<choice_element>     ::= <classical_literal> <COLON> <naf_literals>
                        | <classical_literal> <COLON>
                        | <classical_literal>
<aggregate>          ::= <aggregate_function> <CURLY_OPEN> <aggregate_elements> <CURLY_CLOSE> <binop> <term>
                        | <aggregate_function> <CURLY_OPEN> <aggregate_elements> <CURLY_CLOSE>
                        | <aggregate_function> <CURLY_OPEN> <CURLY_CLOSE> <binop> <term>
                        | <aggregate_function> <CURLY_OPEN> <CURLY_CLOSE>
                        | <term> <binop> <aggregate_function> <CURLY_OPEN> <aggregate_elements> <CURLY_CLOSE> <binop> <term>
                        | <term> <binop> <aggregate_function> <CURLY_OPEN> <aggregate_elements> <CURLY_CLOSE>
                        | <term> <binop> <aggregate_function> <CURLY_OPEN> <CURLY_CLOSE> <binop> <term>
                        | <term> <binop> <aggregate_function> <CURLY_OPEN> <CURLY_CLOSE> 
<aggregate_elements> ::= <aggregate_element> <SEMICOLON> <aggregate_elements>
                        | <aggregate_element>
<aggregate_element>  ::= <COLON> <naf_literals>
                        | <basic_terms> <COLON>
                        | <basic_terms> <COLON> <naf_literals>
                        | <basic_terms>
                        | <COLON>
                        | ""
<aggregate_function> ::= <AGGREGATE_COUNT>
                        | <AGGREGATE_MAX>
                        | <AGGREGATE_MIN>
                        | <AGGREGATE_SUM>
<weight_at_level>    ::= <term> <AT> <term> <COMMA> <terms>
                        | <term> <COMMA> <terms>
                        | <term> <AT> <term>
                        | <term> 
<naf_literals>       ::= <naf_literal> <COMMA> <naf_literals>
                        | <naf_literal> 
<naf_literal>        ::= <NAF> <classical_literal>
                        | <classical_literal>
                        | <builtin_atom>
<classical_literal>  ::= <MINUS> <ID> <PAREN_OPEN> <terms> <PAREN_CLOSE>
                        | <MINUS> <ID> <PAREN_OPEN> <PAREN_CLOSE>
                        | <MINUS> <ID>
                        | <ID> <PAREN_OPEN> <terms> <PAREN_CLOSE>
                        | <ID> <PAREN_OPEN> <PAREN_CLOSE>
                        | <ID>
<builtin_atom>       ::= <term> <binop> <term>
<binop>              ::= <EQUAL>
                        | <UNEQUAL>
                        | <LESS>
                        | <GREATER>
                        | <LESS_OR_EQ>
                        | <GREATER_OR_EQ>
<term_op_repeat>     ::= <arithop> <term> <term_op_repeat>
                        | ""
<terms>              ::= <term> <SEMICOLON> <terms>
                        | <term> <COMMA> <terms>
                        | <term> 
<term>               ::= <ID> <term_op_repeat>
                        | <ID> <PAREN_OPEN> <PAREN_CLOSE> <term_op_repeat>
                        | <ID> <PAREN_OPEN> <terms> <PAREN_CLOSE> <term_op_repeat>
                        | <NUMBER> <term_op_repeat>
                        | <STRING> <term_op_repeat>
                        | <VARIABLE> <term_op_repeat>
                        | <ANONYMOUS_VARIABLE> <term_op_repeat>
                        | <PAREN_OPEN> <term> <PAREN_CLOSE> <term_op_repeat>
                        | <MINUS> <term> <term_op_repeat>
<basic_terms>        ::= <basic_term> <COMMA> <basic_terms>
                        | <basic_term>
<basic_term>         ::= <ground_term>
                        | <variable_term>
<ground_term>        ::= <MINUS> <NUMBER>
                        | <NUMBER>
                        | <SYMBOLIC_CONSTANT>
                        | <STRING> 
<variable_term>      ::= <ANONYMOUS_VARIABLE>
                        | <VARIABLE> 
<arithop>            ::= <PLUS>
                        | <MINUS>
                        | <TIMES>
                        | <DIV>
<ZERO> ::= "0"
<DIGIT_NO_ZERO> ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<DIGIT> ::= <ZERO> | <DIGIT_NO_ZERO>
<ALPHABET_SMALL> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
<ALPHABET_BIG> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
<SYMBOLIC_WITHOUT_STAR> ::= "-" | "_" | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":" | ";" | "<" | "=" | ">" | "\?" | "@" | "\[" | "\]" | "^" | "_" | "\`" | "{" | "|" | "}" | "~" | "TODO Backslash!"
<SYMBOLIC_CONSTANT> ::= "*" | <SYMBOLIC_WITHOUT_STAR>
<UNDERLINE> ::= "_"
<DOUBLE_QUOTE> ::= '\"'
<ALPHANUMERIC_REPEAT> ::= <ALPHABET_SMALL> <ALPHANUMERIC_REPEAT> | <ALPHABET_BIG> <ALPHANUMERIC_REPEAT> | <DIGIT> <ALPHANUMERIC_REPEAT> | <UNDERLINE> <ALPHANUMERIC_REPEAT> | <ALPHABET_SMALL> | <ALPHABET_BIG> | <DIGIT> | <UNDERLINE> | ""
<ID> ::= <ALPHABET_SMALL> <ALPHANUMERIC_REPEAT> | <ALPHABET_SMALL>
<VARIABLE> ::= <ALPHABET_BIG> <ALPHANUMERIC_REPEAT> | <ALPHABET_BIG>
<STRING_CONTENT> ::= <DIGIT> <STRING_CONTENT> | <ALPHABET_SMALL> <STRING_CONTENT> | <ALPHABET_BIG> <STRING_CONTENT>
                        | <UNDERLINE> <STRING_CONTENT> | ""
<STRING> ::= <DOUBLE_QUOTE> <STRING_CONTENT> <DOUBLE_QUOTE> | <DOUBLE_QUOTE> <DOUBLE_QUOTE>
<DIGIT_REPEAT> ::= <DIGIT> <DIGIT_REPEAT> | <DIGIT>
<NUMBER> ::= <DIGIT_NO_ZERO> <DIGIT_REPEAT> | <ZERO> | <DIGIT_NO_ZERO>
<ANONYMOUS_VARIABLE> ::= <UNDERLINE>
<DOT> ::= "."
<COMMA> ::= ","
<QUERY_MARK> ::= "?"
<COLON> ::= ":"
<SEMICOLON> ::= ";"
<OR> ::= "|"
<NAF> ::= "not"
<CONS> ::= ":-"
<WCONS> ::= ":~"
<PLUS> ::= "+"
<MINUS> ::= "-"
<TIMES> ::= "*"
<DIV> ::= "/"
<AT> ::= "@"
<PAREN_OPEN> ::= "("
<PAREN_CLOSE> ::= ")"
<SQUARE_OPEN> ::= "["
<SQUARE_CLOSE> ::= "]"
<CURLY_OPEN> ::= "{"
<CURLY_CLOSE> ::= "}"
<EQUAL> ::= "="
<UNEQUAL> ::= "<>" | "!="
<LESS> ::= "<"
<GREATER> ::= ">"
<LESS_OR_EQ> ::= "<="
<GREATER_OR_EQ> ::= ">="
<AGGREGATE_COUNT> ::= "#COUNT"
<AGGREGATE_MAX> ::= "#max"
<AGGREGATE_MIN> ::= "#min"
<AGGREGATE_SUM> ::= "#sum"
<COMMENT_WITHOUT_STAR_REPEAT> ::= <DIGIT> <COMMENT_REPEAT> | <ALPHABET_SMALL> <COMMENT_REPEAT> | <ALPHABET_BIG> <COMMENT_REPEAT> | <SYMBOLIC_WITHOUT_STAR> <COMMENT_REPEAT> | ""
<COMMENT_REPEAT> ::= <DIGIT> <COMMENT_REPEAT> | <ALPHABET_SMALL> <COMMENT_REPEAT> | <ALPHABET_BIG> <COMMENT_REPEAT> | <SYMBOLIC_CONSTANT> <COMMENT_REPEAT> | ""
<COMMENT_CONTENT> ::= <DIGIT> | <ALPHABET_SMALL> | <ALPHABET_BIG> | <SYMBOLIC_CONSTANT>
<COMMENT> ::= "%" <COMMENT_CONTENT> <COMMENT_REPEAT> <BLANK> | "%" <BLANK>
<MULTI_LINE_COMMENT> ::= "%*" <EOL> | "*%"
<BLANK> ::= <RULE_WS> | ""
<RULE_WS> ::= " " <BLANK> | <EOL> <BLANK> | " " | <EOL>